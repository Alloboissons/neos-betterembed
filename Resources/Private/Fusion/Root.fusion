include: Component/**/*

prototype(Neos.Neos:Page) {
    head {
        stylesheets {
            betterEmbed = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel="stylesheet"
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://BetterEmbed.NeosEmbed/Public/css/defaultStyle.css'
                    }
                }
            }

            betterEmbedPlaceholder = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel="stylesheet"
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://BetterEmbed.NeosEmbed/Public/css/embed.css'
                    }
                }
            }
        }
    }
}


prototype(BetterEmbed.NeosEmbed:Item) < prototype(Neos.Neos:ContentComponent) {

    identifier              = ${'embed-form-' + q(node).property('_identifier')}
    titel                   = ${q(node.properties.record).property('title')}
    url                     = ${q(node.properties.record).property('url')}
    thumbnailUrl            = ${q(node.properties.record).property('thumbnailUrl')}
    thumbnailContentType    = ${q(node.properties.record).property('thumbnailContentType')}
    thumbnailContent        = ${q(node.properties.record).property('thumbnailContent')}
    embedHtml               = ${q(node.properties.record).property('embedHtml')}
    body                    = ${q(node.properties.record).property('body')}
    datePublished           = ${q(node.properties.record).property('publishedAt')}
    authorName              = ${q(node.properties.record).property('authorName')}
    authorImage             = ${q(node.properties.record).property('authorImage')}
    authorUrl               = ${q(node.properties.record).property('authorUrl')}
    itemType                = ${q(node.properties.record).property('itemType')}

    renderer = Neos.Fusion:Case {
        isset {
            condition = ${node.properties.record != null}
            renderer = BetterEmbed.NeosEmbed:ComponentRenderer
        }

        backend {
            condition = ${node.context.inBackend}
            renderer = Neos.Fusion:Join {
                message =  afx`
                    <div class="better-embed-placeholder">
                        <div class="heading">Please set a url in the inspector panel</div>
                    </div>
                `
            }

        }
    }
}

prototype(BetterEmbed.NeosEmbed:ComponentRenderer) < prototype(Neos.Fusion:Case) {
    default {
        condition = true
        renderer = BetterEmbed.NeosEmbed:Component.Default {
            @apply.props = ${props}
        }
    }
}


