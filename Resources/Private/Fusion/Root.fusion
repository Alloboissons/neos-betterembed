include: Component/**/*

prototype(Neos.Neos:Page) {
    head {
        betterEmbedCss = Neos.Fusion:Join {
            css = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel="stylesheet"
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://BetterEmbed.NeosEmbed/Public/css/defaultStyle.css'
                    }
                }
            }
            placeholder = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel="stylesheet"
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://BetterEmbed.NeosEmbed/Public/css/embed.css'
                    }
                }
            }
            js = Neos.Fusion:Tag {
                tagName = 'script'
                attributes {
                    defer = ''
                    type ="text/javascript"
                    src = Neos.Fusion:ResourceUri {
                        path = 'resource://BetterEmbed.NeosEmbed/Public/js/embed.js'
                    }
                }
            }
        }
        betterEmbedCss.@position = 'end'
        betterEmbedCss.@if.render = ${q(documentNode).children('[instanceof Neos.Neos:ContentCollection]').find('[instanceof BetterEmbed.NeosEmbed:Mixin.Include]').is() || q(documentNode).is('[instanceof BetterEmbed.NeosEmbed:Mixin.Include]')}
    }
}

prototype(BetterEmbed.NeosEmbed:Item) < prototype(Neos.Neos:ContentComponent) {

    recordNode              = ${q(node).property('record')}

    identifier              = ${'embed-form-' + q(node).property('_identifier')}
    titel                   = ${q(this.recordNode).property('title')}
    url                     = ${q(this.recordNode).property('url')}
    thumbnail               = ${q(this.recordNode).property('thumbnail')}
    thumbnailUrl            = ${q(this.recordNode).property('thumbnailUrl')}
    thumbnailContentType    = ${q(this.recordNode).property('thumbnailContentType')}
    thumbnailContent        = ${q(this.recordNode).property('thumbnailContent')}
    embedHtml               = ${q(this.recordNode).property('embedHtml')}
    body                    = ${q(this.recordNode).property('body')}
    datePublished           = ${q(this.recordNode).property('publishedAt')}
    authorName              = ${q(this.recordNode).property('authorName')}
    authorImage             = ${q(this.recordNode).property('authorImage')}
    authorUrl               = ${q(this.recordNode).property('authorUrl')}
    itemType                = ${q(this.recordNode).property('itemType')}

    renderer = Neos.Fusion:Case {
        isset {
            condition = ${node.properties.record != null}
            renderer = BetterEmbed.NeosEmbed:ComponentRenderer
        }

        backend {
            condition = ${node.context.inBackend}
            renderer = Neos.Fusion:Join {
                message =  afx`
                    <div class="better-embed-placeholder">
                        <div class="heading">Please set a url in the inspector panel</div>
                    </div>
                `
            }

        }
    }

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.nodeTypeTag('BetterEmbed.NeosEmbed:Record', node)}
        }
    }

//    @process.fix = Neos.Neos:ContentElementWrapping {
//        node = ${node.properties.record ? node.properties.record : node}
//        @position = 'end'
//    }
}

prototype(BetterEmbed.NeosEmbed:ComponentRenderer) < prototype(Neos.Fusion:Case) {
    default {
        condition = true
        renderer = BetterEmbed.NeosEmbed:Component.Default {
            @apply.props = ${props}
        }
    }
}


