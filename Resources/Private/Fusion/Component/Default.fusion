prototype(BetterEmbed.NeosEmbed:Component.Default) < prototype(Neos.Fusion:Component) {

    titel = null
    url = null
    thumbnailUrl = null
    body = null
    datePublished = null
    authorName = null
    authorImage = null
    authorUrl = null
    itemType = null

    renderer = afx`
        <BetterEmbed.NeosEmbed:Component.Default.Tag itemType={props.itemType}>
          <div class="betterembed__item">
            <header>
              <h3 class="betterembed__network">{props.itemType}</h3>
            </header>
            <div class="betterembed__body">
              <div @if.render={props.thumbnailUrl} class="betterembed__media">
                <img src={props.thumbnailUrl} alt="embed image"/>
              </div>
              <h3 @if.render={props.title} class="betterembed__title">{props.title}</h3>
              <div class="betterembed__text">
                {props.body} <a href={props.url} target="_blank" class="betterembed__read-more">read more</a>
              </div>
              <footer class="betterembed__footer">
                <span @if.render={props.authorName} class="betterembed__author">
                  <a href={props.authorUrl} target="_blank">{props.authorName}</a>
                </span>
                <span @if.render={props.datePublished}> {Date.format(props.datePublished, 'd.m.Y')}</span>
              </footer>
            </div>
          </div>
        </BetterEmbed.NeosEmbed:Component.Default.Tag>
    `
}

prototype(BetterEmbed.NeosEmbed:Component.Default.Schema) < prototype(Neos.Fusion:Tag) {
    tagName = ''
}

prototype(BetterEmbed.NeosEmbed:Component.Default.Tag) < prototype(Neos.Fusion:Tag) {
    @context.itemType = ${this.itemType}
    @context.defaultClass = 'betterembed'

    tagName = 'article'
    
    attributes.class = Neos.Fusion:Case {
        twitter {
            condition = ${itemType == 'Twitter'}
            renderer = ${defaultClass + ' betterembed--twitter'}
        }

        youtube {
            condition = ${itemType == 'YouTube'}
            renderer = ${defaultClass + ' betterembed--youtube'}
        }

        facebook {
            condition = ${itemType == 'Facebook'}
            renderer = ${defaultClass + ' betterembed--facebook'}
        }

        github {
            condition = ${itemType == 'GitHub'}
            renderer = ${defaultClass + ' betterembed--github'}
        }

        default {
            condition = true
            renderer = ${defaultClass}

        }
    }
}



