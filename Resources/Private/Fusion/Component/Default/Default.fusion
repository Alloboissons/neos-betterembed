prototype(BetterEmbed.NeosEmbed:Component.Default) < prototype(Neos.Fusion:Component) {
    title = null
    url = null
    thumbnail = null
    body = null
    datePublished = null
    authorName = null
    authorUrl = null
    itemType = null
    embedHtml = null
    thumbnailAlternativeText = ${Translation.translate('thumbnail.alternativeText', 'Thumbnail from the embeded URL', [], null, 'BetterEmbed.NeosEmbed')}

    titleTagName = 'h2'

    modifier = null

    betterEmbedLogo = ${File.readFile('resource://BetterEmbed.NeosEmbed/Private/Assets/Logo.svg')}

    // Translation strings
    msgGdpr = ${Translation.translate('text.msgGdpr', '', [], null, 'BetterEmbed.NeosEmbed')}
    msgClick = ${Translation.translate('text.msgClick', 'With a click on the link below, the original content will be loaded. This can include remote content and you can possibly be tracked from the original provider.', [], null, 'BetterEmbed.NeosEmbed')}
    labelOk = ${Translation.translate('text.labelOk', 'show original content', [], null, 'BetterEmbed.NeosEmbed')}
    labelCancel = ${Translation.translate('text.labelCancel', 'close', [], null, 'BetterEmbed.NeosEmbed')}
    readMore = ${Translation.translate('text.readMore', 'read more', [], null, 'BetterEmbed.NeosEmbed')}
    showOriginal = ${Translation.translate('text.showOriginal', 'show original', [], null, 'BetterEmbed.NeosEmbed')}
    hideOriginal = ${Translation.translate('text.hideOriginal', 'hide original', [], null, 'BetterEmbed.NeosEmbed')}

    @if.hasItemType = ${this.itemType}

    renderer = afx`
        <BetterEmbed.NeosEmbed:Component.Default.Fragment.Tag
            namespace='betterembed'
            modifier={props.modifier}
            itemType={props.itemType}
            data-embed-html={props.embedHtml ? Json.stringify(props.embedHtml) : null}
        >
            <div class='betterembed__top'>
                <div class='betterembed__network'>{props.itemType}</div>
                <button @if.set={props.embedHtml} type='button' class='betterembed-switch js-betterembed-switch'>
                    <span class='betterembed-switch__show'>{props.showOriginal}</span>
                    <span class='betterembed-switch__hide'>{props.hideOriginal}</span>
                    {props.betterEmbedLogo}
                </button>
            </div>
            <div @if.set={props.embedHtml} class='betterembed__dialog'>
                <p @if.set={props.msgClick}>{props.msgClick}</p>
                <p @if.set={props.msgGdpr}>{props.msgGdpr}</p>
                <button type='button' class='betterembed__button betterembed__button--primary js-betterembed-load-remote'>{props.labelOk}</button>
                <button type='button' class='betterembed__button betterembed__button--secondary js-betterembed-close'>{props.labelCancel}</button>
            </div>
            <article class='betterembed__body'>
                <header @if.set={props.title && props.titleTagName} class='betterembed__header'>
                    <Neos.Fusion:Tag tagName={props.titleTagName}>
                        {props.title}
                    </Neos.Fusion:Tag>
                </header>
                <figure @if.set={props.thumbnail} class='betterembed__media'>
                    <Neos.Neos:ImageTag asset={props.thumbnail} attributes.alt={props.thumbnailAlternativeText} />
                </figure>
                <p class='betterembed__text' @if.set={props.body || props.readMore}>
                    {props.body} <a @if.set={props.url} href={props.url} class='betterembed__read-more'>{props.readMore}</a>
                </p>
                <footer @if.set={props.authorName || props.datePublished} class='betterembed__footer'>
                    <a @if.set={props.authorName} rel='author' href={props.authorUrl} class='betterembed__author'>
                        {props.authorName}
                    </a>
                    {' '}
                    <time @if.set={props.datePublished} datetime={Date.format(props.datePublished, 'Y-m-d')}>{Date.format(props.datePublished, 'd.m.Y')}</time>
                </footer>
            </article>
            <div @if.set={props.embedHtml} class='betterembed__embed js-betterembed-embed'></div>
        </BetterEmbed.NeosEmbed:Component.Default.Fragment.Tag>
    `
}
