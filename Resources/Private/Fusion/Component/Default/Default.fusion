prototype(BetterEmbed.NeosEmbed:Component.Default) < prototype(Neos.Fusion:Component) {

    titel = null
    url = null
    thumbnailUrl = null
    thumbnail = null
    body = null
    datePublished = null
    authorName = null
    authorImage = null
    authorUrl = null
    itemType = null
    embedHtml = null

    modifier = null


    // Translation strings
    msgGdpr = ${Translation.translate('text.msgGdpr', '', [], null, 'BetterEmbed.NeosEmbed')}
    msgClick = ${Translation.translate('text.msgClick', 'With a click on the link below, the original content will be loaded. This can include remote content and you can possibly be tracked from the original provider.', [], null, 'BetterEmbed.NeosEmbed')}
    labelOk = ${Translation.translate('text.labelOk', 'show original content', [], null, 'BetterEmbed.NeosEmbed')}
    labelCancel = ${Translation.translate('text.labelCancel', 'close', [], null, 'BetterEmbed.NeosEmbed')}
    readMore = ${Translation.translate('text.readMore', 'read more', [], null, 'BetterEmbed.NeosEmbed')}
    showOriginal = ${Translation.translate('text.showOriginal', 'show original', [], null, 'BetterEmbed.NeosEmbed')}
    hideOriginal = ${Translation.translate('text.hideOriginal', 'hide original', [], null, 'BetterEmbed.NeosEmbed')}

    renderer = afx`
        <BetterEmbed.NeosEmbed:Component.Default.Fragment.Tag
            namespace='betterembed'
            modifier={props.modifier}
            itemType={props.itemType}
            data-embed-html={props.embedHtml ? Json.stringify(props.embedHtml) : null}
        >
            <button type="button" class="betterembed__show-original-element js-betterembed-show-message"></button>
            <div class="betterembed__item">
                <header>
                    <h3 class="betterembed__network">{props.itemType}</h3>
                </header>
                <div class="betterembed__body">
                    <div class="betterembed__msg">
                        <p>
                            {props.msgClick}
                        </p>
                        <p @if.isset={props.msgGdpr}>
                            <strong>{props.msgGdpr}</strong>
                        </p>
                        <button type="button" class="betterembed__msg-button-primary js-betterembed-load-remote">{props.labelOk}</button>
                        <button type="button" class="betterembed__msg-button-secondary js-betterembed-close">{props.labelCancel}</button>
                    </div>
                    <div @if.render={props.thumbnailUrl} class="betterembed__media">
                        <Neos.Neos:ImageTag asset={props.thumbnail} alt="embed image"/>
                    </div>
                    <h3 @if.render={props.title} class="betterembed__title">{props.title}</h3>
                    <div class="betterembed__text">
                        {props.body} <a href={props.url} class="betterembed__read-more">{props.readMore}</a>
                    </div>
                    <footer class="betterembed__footer">
                        <span @if.render={props.authorName} class="betterembed__author">
                          <a href={props.authorUrl}>{props.authorName}</a>
                        </span>
                        <span @if.render={props.datePublished}> {Date.format(props.datePublished, 'd.m.Y')}</span>
                    </footer>
                </div>
            </div>
            <div class="betterembed__embed"></div>
        </BetterEmbed.NeosEmbed:Component.Default.Fragment.Tag>
    `
}
